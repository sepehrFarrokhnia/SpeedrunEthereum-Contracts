// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.20;

import "@openzeppelin/contracts/access/Ownable.sol";
import "./MyUSD.sol";
import "./Oracle.sol";
import "./MyUSDStaking.sol";

error Engine__InvalidAmount();
error Engine__UnsafePositionRatio();
error Engine__NotLiquidatable();
error Engine__InvalidBorrowRate();
error Engine__NotRateController();
error Engine__InsufficientCollateral();
error Engine__TransferFailed();

contract MyUSDEngine is Ownable {
    uint256 private constant COLLATERAL_RATIO = 150; // 150% collateralization required
    uint256 private constant LIQUIDATOR_REWARD = 10; // 10% reward for liquidators
    uint256 private constant SECONDS_PER_YEAR = 365 days;
    uint256 private constant PRECISION = 1e18;
    uint256 private constant BORROW_RATE_PRECISION = 1e4;
    uint256 private constant COLLATERAL_RATIO_PRECISION = 1e2;

    MyUSD private i_myUSD;
    Oracle private i_oracle;
    MyUSDStaking private i_staking;
    address private i_rateController;

    uint256 public borrowRate; // Annual interest rate for borrowers in basis points (1% = 100)

    // Total debt shares in the pool
    uint256 public totalDebtShares;

    // Exchange rate between debt shares and MyUSD (1e18 precision)
    uint256 public debtExchangeRate;
    uint256 public lastUpdateTime;

    mapping(address => uint256) public s_userCollateral;
    mapping(address => uint256) public s_userDebtShares;

    event CollateralAdded(address indexed user, uint256 indexed amount, uint256 price);
    event CollateralWithdrawn(address indexed withdrawer, uint256 indexed amount, uint256 price);
    event BorrowRateUpdated(uint256 newRate);
    event DebtSharesMinted(address indexed user, uint256 amount, uint256 shares);
    event DebtSharesBurned(address indexed user, uint256 amount, uint256 shares);
    event Liquidation(
        address indexed user,
        address indexed liquidator,
        uint256 amountForLiquidator,
        uint256 liquidatedUserDebt,
        uint256 price
    );

    modifier greaterThanZero(uint256 amount) {
        if (amount == 0) revert Engine__InvalidAmount();
        _;
    }

    modifier onlyRateController() {
        if (msg.sender != i_rateController) revert Engine__NotRateController();
        _;
    }

    constructor(
        address _oracle,
        address _myUSDAddress,
        address _stakingAddress,
        address _rateController
    ) Ownable(msg.sender) {
        i_oracle = Oracle(_oracle);
        i_myUSD = MyUSD(_myUSDAddress);
        i_staking = MyUSDStaking(_stakingAddress);
        i_rateController = _rateController;
        lastUpdateTime = block.timestamp;
        debtExchangeRate = PRECISION; // 1:1 initially
    }

    // Checkpoint 2: Depositing Collateral & Understanding Value
    function addCollateral() public payable greaterThanZero(msg.value) {
        s_userCollateral[msg.sender] = msg.value;
        emit CollateralAdded(msg.sender, msg.value, i_oracle.getETHMyUSDPrice());
    }

    function calculateCollateralValue(address user) public view returns (uint256) {
        return (s_userCollateral[user] * i_oracle.getETHMyUSDPrice()) / PRECISION;
    }

    // Checkpoint 3: Interest Calculation System
    function _getCurrentExchangeRate() internal view returns (uint256) {
        if (totalDebtShares == 0) return debtExchangeRate;

        uint256 timeElapsed = block.timestamp - lastUpdateTime;
        if (timeElapsed == 0 || borrowRate == 0) return debtExchangeRate;

        uint256 totalDebtValue = (totalDebtShares * debtExchangeRate) / PRECISION;
        uint256 interest = (totalDebtValue * borrowRate * timeElapsed) / (SECONDS_PER_YEAR * BORROW_RATE_PRECISION);

        return debtExchangeRate + (interest * PRECISION) / totalDebtShares;
    }

    function _accrueInterest() internal {
        if (totalDebtShares == 0) {
            lastUpdateTime = block.timestamp;
            return;
        }
        debtExchangeRate = _getCurrentExchangeRate();
        lastUpdateTime = block.timestamp;
    }

    function _getMyUSDToShares(uint256 amount) internal view greaterThanZero(amount) returns (uint256) {
        return (amount * PRECISION) / _getCurrentExchangeRate();
    }

    // Checkpoint 4: Minting MyUSD & Position Health
    function getCurrentDebtValue(address user) public view returns (uint256) {
        if (s_userDebtShares[user] == 0) return 0;
        return (s_userDebtShares[user] * _getCurrentExchangeRate()) / PRECISION;
    }

    function calculatePositionRatio(address user) public view returns (uint256) {
        uint256 userDebtShareValue = getCurrentDebtValue(user);
        if (userDebtShareValue == 0) return type(uint256).max;

        uint256 userCollateralValue = calculateCollateralValue(user);

        return (userCollateralValue * PRECISION * COLLATERAL_RATIO_PRECISION) / userDebtShareValue;
    }

    function _validatePosition(address user) internal view {
        if (calculatePositionRatio(user) < COLLATERAL_RATIO * PRECISION) {
            revert Engine__UnsafePositionRatio();
        }
    }

    function mintMyUSD(uint256 mintAmount) public greaterThanZero(mintAmount) {
        uint256 mintShare = _getMyUSDToShares(mintAmount);
        s_userDebtShares[msg.sender] += mintShare;
        totalDebtShares += mintShare;
        _validatePosition(msg.sender);

        if (!i_myUSD.mintTo(msg.sender, mintAmount)) {
            revert Engine__TransferFailed();
        }

        emit DebtSharesMinted(msg.sender, mintAmount, mintShare);
    }

    // Checkpoint 5: Accruing Interest & Managing Borrow Rates
    function setBorrowRate(uint256 newRate) external onlyRateController {
        if (newRate < i_staking.savingsRate()) {
            revert Engine__InvalidBorrowRate();
        }
        _accrueInterest();
        borrowRate = newRate;
        emit BorrowRateUpdated(newRate);
    }

    // Checkpoint 6: Repaying Debt & Withdrawing Collateral
    function repayUpTo(uint256 amount) public greaterThanZero(amount) {
        uint256 debtValue = getCurrentDebtValue(msg.sender);
        require(debtValue != 0, "no debt to pay");

        if (debtValue < amount) amount = debtValue;
        require(i_myUSD.balanceOf(msg.sender) >= amount, "insufficient found");
        i_myUSD.burnFrom(msg.sender, amount);

        s_userDebtShares[msg.sender] -= _getMyUSDToShares(amount);

        emit DebtSharesBurned(msg.sender, amount, _getMyUSDToShares(amount));
    }

    function withdrawCollateral(uint256 amount) external greaterThanZero(amount) {
        if (amount > s_userCollateral[msg.sender]) {
            revert Engine__InsufficientCollateral();
        }
        s_userCollateral[msg.sender] -= amount;
        _validatePosition(msg.sender);

        (bool send, ) = msg.sender.call{ value: amount }("");
        if (!send) revert Engine__TransferFailed();

        emit CollateralWithdrawn(msg.sender, amount, i_oracle.getETHMyUSDPrice());
    }

    // Checkpoint 7: Liquidation - Enforcing System Stability
    function isLiquidatable(address user) public view returns (bool) {
        return calculatePositionRatio(user) < COLLATERAL_RATIO * PRECISION;
    }

    function liquidate(address user) external {
        if (!isLiquidatable(user)) {
            revert Engine__NotLiquidatable();
        }

        uint256 userDebtValue = getCurrentDebtValue(user);

        require(i_myUSD.balanceOf(msg.sender) >= userDebtValue, "insufficient found");
        i_myUSD.burnFrom(msg.sender, userDebtValue);

        uint256 userDebt = s_userDebtShares[user];
        s_userDebtShares[user] = 0;

        uint256 paidInEth = (userDebtValue * i_oracle.getETHMyUSDPrice()) / PRECISION;
        uint256 liquidReward = (paidInEth * LIQUIDATOR_REWARD) / 100;
        uint256 ethToPay = paidInEth + liquidReward;

        if (s_userCollateral[user] < ethToPay) ethToPay = s_userCollateral[user];

        (bool send, ) = msg.sender.call{ value: ethToPay }("");

        if (!send) revert Engine__TransferFailed();
        emit Liquidation(user, msg.sender, ethToPay, userDebt, i_oracle.getETHMyUSDPrice());
    }
}
