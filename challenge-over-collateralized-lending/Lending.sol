// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.20;

import "@openzeppelin/contracts/access/Ownable.sol";
import "./Corn.sol";
import "./CornDEX.sol";

error Lending__InvalidAmount();
error Lending__TransferFailed();
error Lending__UnsafePositionRatio();
error Lending__BorrowingFailed();
error Lending__RepayingFailed();
error Lending__PositionSafe();
error Lending__NotLiquidatable();
error Lending__InsufficientLiquidatorCorn();
error Lending__FlashLoanFailed();

contract Lending is Ownable {
    uint256 private constant COLLATERAL_RATIO = 120; // 120% collateralization required
    uint256 private constant LIQUIDATOR_REWARD = 10; // 10% reward for liquidators

    uint256 private constant PRECISION = 1e18;

    Corn private i_corn;
    CornDEX private i_cornDEX;

    mapping(address => uint256) public s_userCollateral; // User's collateral balance
    mapping(address => uint256) public s_userBorrowed; // User's borrowed corn balance

    event CollateralAdded(address indexed user, uint256 indexed amount, uint256 price);
    event CollateralWithdrawn(address indexed user, uint256 indexed amount, uint256 price);
    event AssetBorrowed(address indexed user, uint256 indexed amount, uint256 price);
    event AssetRepaid(address indexed user, uint256 indexed amount, uint256 price);
    event Liquidation(
        address indexed user,
        address indexed liquidator,
        uint256 amountForLiquidator,
        uint256 liquidatedUserDebt,
        uint256 price
    );

    constructor(address _cornDEX, address _corn) Ownable(msg.sender) {
        i_cornDEX = CornDEX(_cornDEX);
        i_corn = Corn(_corn);
        i_corn.approve(address(this), type(uint256).max);
    }

    modifier greatherThanZero(uint256 value) {
        if (value == 0) {
            revert Lending__InvalidAmount();
        }
        _;
    }

    /**
     * @notice Allows users to add collateral to their account
     */
    function addCollateral() public payable greatherThanZero(msg.value) {
        s_userCollateral[msg.sender] += msg.value;
        emit CollateralAdded(msg.sender, msg.value, i_cornDEX.currentPrice());
    }

    /**
     * @notice Allows users to withdraw collateral as long as it doesn't make them liquidatable
     * @param amount The amount of collateral to withdraw
     */
    function withdrawCollateral(uint256 amount) public greatherThanZero(amount) {
        if (s_userCollateral[msg.sender] < amount) {
            revert Lending__InvalidAmount();
        }
        s_userCollateral[msg.sender] -= amount;
        _validatePosition(msg.sender);
        (bool send, ) = msg.sender.call{ value: amount }("");
        if (!send) {
            revert Lending__TransferFailed();
        }
        emit CollateralWithdrawn(msg.sender, amount, i_cornDEX.currentPrice());
    }

    /**
     * @notice Calculates the total collateral value for a user based on their collateral balance
     * @param user The address of the user to calculate the collateral value for
     * @return uint256 The collateral value
     */
    function calculateCollateralValue(address user) public view returns (uint256) {
        return (s_userCollateral[user] * i_cornDEX.currentPrice()) / PRECISION;
    }

    /**
     * @notice Calculates the position ratio for a user to ensure they are within safe limits
     * @param user The address of the user to calculate the position ratio for
     * @return uint256 The position ratio
     */
    function _calculatePositionRatio(address user) internal view returns (uint256) {
        if (s_userBorrowed[user] == 0) return type(uint256).max;

        return ((calculateCollateralValue(user) * PRECISION * 100) / s_userBorrowed[user]);
    }

    /**
     * @notice Checks if a user's position can be liquidated
     * @param user The address of the user to check
     * @return bool True if the position is liquidatable, false otherwise
     */
    function isLiquidatable(address user) public view returns (bool) {
        return _calculatePositionRatio(user) < COLLATERAL_RATIO * PRECISION;
    }

    /**
     * @notice Internal view method that reverts if a user's position is unsafe
     * @param user The address of the user to validate
     */
    function _validatePosition(address user) internal view {
        if (isLiquidatable(user)) {
            revert Lending__UnsafePositionRatio();
        }
    }

    function getMaxBorrowAmount(uint256 amount) public view greatherThanZero(amount) returns (uint256 maxBorrow) {
        uint256 amountInCorn = (amount * i_cornDEX.currentPrice()) / PRECISION;
        maxBorrow = (amountInCorn * 100) / COLLATERAL_RATIO;
    }

    function getMaxWithdrawableCollateral(address user) public view returns (uint256) {
        if(s_userBorrowed[user] == 0) return s_userCollateral[user];

        uint256 maxBorrow = getMaxBorrowAmount(s_userCollateral[user]);
        if (maxBorrow == s_userBorrowed[user]) return 0;
        maxBorrow -= s_userBorrowed[user];

        uint256 maxWithdrawEth = maxBorrow * PRECISION / i_cornDEX.currentPrice();

        return maxWithdrawEth * COLLATERAL_RATIO / 100;
    }

    /**
     * @notice Allows users to borrow corn based on their collateral
     * @param borrowAmount The amount of corn to borrow
     */
    function borrowCorn(uint256 borrowAmount) public greatherThanZero(borrowAmount) {
        s_userBorrowed[msg.sender] += borrowAmount;
        _validatePosition(msg.sender);
        if (!i_corn.transfer(msg.sender, borrowAmount)) {
            revert Lending__TransferFailed();
        }
        emit AssetBorrowed(msg.sender, borrowAmount, i_cornDEX.currentPrice());
    }

    /**
     * @notice Allows users to repay corn and reduce their debt
     * @param repayAmount The amount of corn to repay
     */
    function repayCorn(uint256 repayAmount) public greatherThanZero(repayAmount) {
        if (repayAmount > s_userBorrowed[msg.sender]) {
            revert Lending__InvalidAmount();
        }
        if (i_corn.allowance(msg.sender, address(this)) < repayAmount) {
            revert Lending__TransferFailed();
        }
        if (!i_corn.transferFrom(msg.sender, address(this), repayAmount)) {
            revert Lending__TransferFailed();
        }
        s_userBorrowed[msg.sender] -= repayAmount;
        emit AssetRepaid(msg.sender, repayAmount, i_cornDEX.currentPrice());
    }

    /**
     * @notice Allows liquidators to liquidate unsafe positions
     * @param user The address of the user to liquidate
     * @dev The caller must have enough CORN to pay back user's debt
     * @dev The caller must have approved this contract to transfer the debt
     */
    function liquidate(address user) public {
        if (!isLiquidatable(user)) {
            revert Lending__NotLiquidatable();
        }
        uint256 userBorrow = s_userBorrowed[user];

        if (i_corn.balanceOf(msg.sender) < userBorrow) {
            revert Lending__InsufficientLiquidatorCorn();
        }
        if (i_corn.allowance(msg.sender, address(this)) < userBorrow) {
            revert Lending__TransferFailed();
        }

        if (!i_corn.transferFrom(msg.sender, address(this), userBorrow)) {
            revert Lending__TransferFailed();
        }

        s_userBorrowed[user] = 0;

        uint256 userBorrowedInEth = i_cornDEX.calculateXInput(
            userBorrow,
            address(i_cornDEX).balance,
            i_corn.balanceOf(address(i_cornDEX))
        );
        uint256 liquidiationReward = (userBorrowedInEth / LIQUIDATOR_REWARD) * 100;
        uint256 ethToPay = userBorrowedInEth + liquidiationReward;

        if (s_userCollateral[user] < ethToPay) {
            ethToPay = s_userCollateral[user];
        }

        s_userCollateral[user] -= ethToPay;

        (bool send, ) = msg.sender.call{ value: ethToPay }("");
        if (!send) {
            revert Lending__TransferFailed();
        }
        emit Liquidation(user, msg.sender, ethToPay, userBorrow, i_cornDEX.currentPrice());
    }

    function flashLoan(
        IFlashLoanRecipient _recipient,
        uint256 _amount,
        address _extraParam
    ) public greatherThanZero(_amount) {
        if (i_corn.balanceOf(address(this)) < _amount) {
            revert Lending__InvalidAmount();
        }
        if (!i_corn.transfer(address(_recipient), _amount)) {
            revert Lending__TransferFailed();
        }
        if (!_recipient.executeOperation(_amount, msg.sender, _extraParam)) {
            revert Lending__FlashLoanFailed();
        }
        if (i_corn.allowance(address(_recipient), address(this)) < _amount) {
            revert Lending__TransferFailed();
        }
        if (!i_corn.transferFrom(address(_recipient), address(this), _amount)) {
            revert Lending__TransferFailed();
        }
    }
}

interface IFlashLoanRecipient {
    function executeOperation(uint256 amount, address initiator, address extraParam) external returns (bool);
}
